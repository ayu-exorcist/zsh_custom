#!/bin/zsh
0=${(%):-%x}
ZSH_CUSTOM=${ZSH_CUSTOM:-$0:A:h:h}
ZFUNCDIR=$ZSH_CUSTOM/functions
autoload -Uz $ZFUNCDIR/autoload-dir
autoload-dir $ZFUNCDIR

if (( ! $+functions[zsh-defer] )); then
  function zsh-defer {
    source $ZSH_CUSTOM/.external/romkatv/zsh-defer/zsh-defer.plugin.zsh
    zsh-defer "$@"
  }
fi

() {
  # clone plugins if necessary
  local lastcloned="$ZSH_CUSTOM/.external/.lastcloned"
  if [[ ! $lastcloned -nt $ZSH_CUSTOM/plugins/zplugins.txt ]]; then
    local -a repos=("${(f)"$(<$ZSH_CUSTOM/plugins/zplugins.txt)"}")
    repos=(${repos:#\#*})
    plugin-clone $repos
    date '+%Y-%m-%d %H:%M:%S' >"$lastcloned"
  fi

  # update plugins weekly
  local lastupdated="$ZSH_CUSTOM/.external/.lastupdated"
  [[ -f "$lastupdated" ]] || touch "$lastupdated"
  local -a lufiles=($lastupdated(Nmw-1))
  if ! (( $#lufiles )); then
    plugin-update >/dev/null
    date '+%Y-%m-%d %H:%M:%S' >"$lastupdated"
  fi
}
