#!/bin/zsh
##? repo - Deal with git repos

function __repo_help {
  emulate -L zsh; setopt local_options
  echo "repo - Deal with git repos"
  echo ""
  echo "commands:"
  echo "  help   print this help"
  echo "  home   print repo home"
  echo "  ls     print repo list"
  echo "  rm     remove repo"
  echo "  in     install repo"
  echo "  up     update repos"
  echo ""
  echo "examples:"
  echo "  repo in <repos.txt"
}

function __repo_home {
  emulate -L zsh; setopt local_options
  if [[ -n "$REPO_HOME" ]]; then
    echo $REPO_HOME
  else
    echo ${XDG_CACHE_HOME:-$HOME/.cache}/repos
  fi
}

function __repo_update {
  emulate -L zsh; setopt local_options no_monitor
  echo "Checking for updates..."
  local repo
  local -a repos
  (( $# > 0 )) && repos=($@) || repos=($(repo ls))
  for repo in $repos; do
    echo "$repo..."
    __repo_pull $repo &
  done
  wait
  echo "Updates complete."
}

function __repo_pull {
  emulate -L zsh; setopt local_options
  local repodir=$(__repo_home)/$1
  local oldsha=$(command git -C $repodir rev-parse --short HEAD)
  command git -C $repodir pull --quiet --ff --depth 1 --rebase --autostash
  local newsha=$(command git -C $repodir rev-parse --short HEAD)
  if [ $oldsha != $newsha ]; then
    echo "Repo updated: "$1" ($oldsha->$newsha)"
  fi
}

function __repo_clone {
  local repo=$1
  local repodir=$(__repo_home)/$repo
  if [ -d $repodir ]; then
    echo "Found $repo..."
    return
  fi

  echo "Cloning $repo..."
  if [[ $repo == sorin-ionescu/prezto ]]; then
    command git clone --quiet --depth 1 \
      https://github.com/$repo $repodir
  else
    command git clone --quiet --depth 1 --recursive --shallow-submodules \
      https://github.com/$repo $repodir
  fi

  local init=$repodir/${repo:t}.plugin.zsh
  if [[ ! -e $init ]]; then
    local -a initfiles=($repodir/*.{plugin.zsh,zsh-theme,zsh,sh}(N))
    (( $#initfiles )) && ln -sf $initfiles[1] $init
  fi
  if [[ $repo == sorin-ionescu/prezto ]]; then
    for init in $repodir/modules/*/init.zsh; do
      ln -sf $init $init:h/${init:h:t}.plugin.zsh
    done
  fi
  echo "Cloned $repo."
}

function __repo_list {
  emulate -L zsh
  setopt local_options extended_glob no_monitor
  local repo err=1
  for repo in $(__repo_home)/*/*/.git(/N); do
    echo ${repo:h:h:t}/${repo:h:t}
    err=0
  done
  return $err
}

function __repo_install {
  emulate -L zsh; setopt local_options no_monitor
  local repo
  for repo in $@; do
    __repo_clone $repo &
  done
  wait
}

function __repo_remove {
  emulate -L zsh; setopt local_options
  local err=0
  local repo_home=$(__repo_home)
  for repo in $@; do
    if ! [[ "$repo_home" =~ $HOME/* ]]; then
      echo >&2 "repo: \$REPO_HOME not set correctly '$repo_home'."
      return 2
    elif ! [[ -d $repo_home/$repo ]]; then
      echo "Repo not found '$repo'."
      err=1
    else
      command rm -rf -- $repo_home/$repo
    fi
  done
  return $err
}

function repo {
  emulate -L zsh
  setopt local_options extended_glob no_monitor

  (( $# )) || {
    echo >&2 "repo: Expecting command argument."
    return 1
  }

  local err=0
  local cmd=$1; shift

  # piped/redirected input
  if ! [ -t 0 ]; then
    local -a args=($@)
    local data
    while IFS= read -r data || [ -n "$data" ]; do
      data=${data%\#*}
      args+=($data)
    done
    set -- $args
  fi

  case "$cmd" in
    help|-h|--help)
      __repo_help
    ;;
    home)
      __repo_home
    ;;
    ls|list)
      __repo_list
      err=$?
    ;;
    up|update)
      __repo_update "$@"
      err=$?
    ;;
    in|install)
      __repo_install "$@"
      err=$?
    ;;
    rm|del)
      __repo_remove "$@"
    ;;
    *)
      echo >&2 "repo: Unknown command '"$cmd"'"
      err=1
    ;;
  esac

  return $err
}
repo "$@"
